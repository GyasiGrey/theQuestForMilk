//menu to choose buy, sell, or cancel
void OpenShop(int itm1, int itm2, int itm3, int itm4, int itm5)
{
	int bkg = duplicateimage(screen);

	int done;

	int pointerpos;
	int win = GenerateWindow(70, 40);
	
	done = 0;

	pointerpos=0;
	while(!done)
	{
		DrawMenu(win, 240, 150, "Shop");
		PrintString(256, 158, screen, nFont, "Buy");
		PrintString(256, 166, screen, nFont, "Sell");
		PrintString(256, 174, screen, nFont, "Cancel");
		
		TBlit(246, 158 + (8 * pointerpos), nPointer, screen);
		ShowPage();
		
		if(up)
		{
			if(pointerpos > 0)
			{
				pointerpos--;
				PlaySound(nMenuMoveSound, 100);
			}
			unpress(5);
		}
		else if(down)
		{
			if(pointerpos < 2)
			{
				pointerpos++;
				PlaySound(nMenuMoveSound, 100);
			}
			unpress(6);
		}
		else if(b1)
		{
			if(pointerpos==0)
			{
				unpress(1);
				Blit(0, 0, bkg, screen);
				ShowPage();
				BuyStuff(itm1, itm2, itm3, itm4, itm5);
				done=1;
			}
			else if(pointerpos==1)
			{
				unpress(1);
				Blit(0, 0, bkg, screen);
				ShowPage();
				SellStuff();
				done=1;
			}
			else if(pointerpos==2)
			{
				done=1;
				unpress(1);
			}
		}
		else if(b2)
		{
			done=1;
			unpress(1);
		}
	}
	
	FreeImage(bkg);
}

//after the OpenShop menu, user chose buy
void BuyStuff(int itm1, int itm2, int itm3, int itm4, int itm5)
{
	int bkg = duplicateimage(screen);
	
	int win = GenerateWindow(170, 75);
	int miniwin = GenerateWindow(70, 20);
	int itemmenudesc = GenerateWindow(200, 20);
	int equipablewin = GenerateWindow(70, 50);
	int pointerpos;

	int done;
	int numshopitems;
	int itembought;
	int itmpointingat;
	
	int itemindex;
	
	int i;
	

	nMenuCanOpen=0;
	
	pointerpos=0;
	
	Render();


	while(!done)
	{
		numshopitems=0;	
		
		Blit(0, 0, bkg, screen);
		
		DrawMenu(win, 35, 20, "Shop");
		DrawMenu(miniwin, 205, 20, "");
		DrawMenu(miniwin, 205, 40, ""); //second miniwindow is for bling
		DrawMenu(itemmenudesc, 35, 110, "Description");
		DrawMenu(equipablewin, 50, 140, "Can Equip");
		
		PrintString(213, 28, screen, nFont, "Bling:");
		PrintString(250, 28, screen, nFont, str(nBling));
		
		PrintString(213, 48, screen, nFont, "Owned:");
		
		if(pointerpos == 0)
		{
			itmpointingat = itm1;
		}
		else if(pointerpos == 1)
		{
			itmpointingat = itm2;
		}
		else if(pointerpos == 2)
		{
			itmpointingat = itm3;
		}
		else if(pointerpos == 3)
		{
			itmpointingat = itm4;
		}
		else if(pointerpos == 4)
		{
			itmpointingat = itm5;
		}
		
		itemindex = HasItem(itmpointingat);
		if(itemindex >= 0)
			PrintString(251, 48, screen, nFont, str(nItemCount[itemindex]));
		else
			PrintString(251, 48, screen, nFont, "0");
		
		if(itm1 >= 0)
		{
			PrintString(43, 28, screen, nFont, Items[itm1].sName);
			PrintString(160, 28, screen, nFont, str(Items[itm1].nCost));
			numshopitems++;
		}
		
		if(itm2 >= 0)
		{
			PrintString(43, 36, screen, nFont, Items[itm2].sName);
			PrintString(160, 36, screen, nFont, str(Items[itm2].nCost));
			numshopitems++;
		}
		
		if(itm3 >= 0)
		{
			PrintString(43, 44, screen, nFont, Items[itm3].sName);
			PrintString(160, 44, screen, nFont, str(Items[itm3].nCost));
			numshopitems++;
		}
		
		if(itm4 >= 0)
		{
			PrintString(43, 52, screen, nFont, Items[itm4].sName);
			PrintString(160, 52, screen, nFont, str(Items[itm4].nCost));
			numshopitems++;
		}
		
		if(itm5 >= 0)
		{
			PrintString(43, 60, screen, nFont, Items[itm5].sName);
			PrintString(160, 60, screen, nFont, str(Items[itm5].nCost));
			numshopitems++;
		}
			
		TBlit(38, 28+(8*pointerpos), nPointer, screen);
		
		PrintString(43, 118, screen, nFont, Items[itmpointingat].sDescription);
	
		//text for the can equip menu
		for(i=0;i<3;i++)
		{
			if(nParty[i] >= 0)
			{
				if(Items[itmpointingat].nCanEquip[nParty[i]])
				{
					PrintString(58, 148 + (i*15), screen, nFont, BatChars[nParty[i]].sName);
				}
			}
		}

		
		ShowPage();
		
		if(up)
		{
			if(pointerpos > 0)
			{
				pointerpos--;
				playsound(nMenuMoveSound, 100);
				unpress(5);
			}
		}
		
		if(down)
		{
			if(pointerpos < numshopitems-1)
			{
				pointerpos++;
				playsound(nMenuMoveSound, 100);
				unpress(6);
			}
		}
		
		if(b1)
		{
			if(pointerpos==0)
				itembought=itm1;
			if(pointerpos==1)
				itembought=itm2;
			if(pointerpos==2)
				itembought=itm3;
			if(pointerpos==3)
				itembought=itm4;
			if(pointerpos==4)
				itembought=itm5;

			//you know, I might just want to give itembought a value before I use it....
			if(nBling >= Items[itembought].nCost)
			{
				playsound(nMenuClickSound, 100);
				//show the "your sure you wanna buy this crap?" window
				if(ChoiceMenu("Buy?", 70, 45))
				{
					//take the money
					nBling -= Items[itembought].nCost;
					//give me the item
					GetItemNoText(itembought, 1);
				}
			}
			else
			{
				//you ain't got no bling playa
				PlaySound(nErrorSound, 100);
			}
			unpress(1);
		}
		
		if(b2)
		{
			done=1;
		}
	}
	
	FreeImage(win);
	FreeImage(miniwin);
	FreeImage(itemmenudesc);
	FreeImage(equipablewin);
	
	nMenuCanOpen=1;
}

//lets the user sell stuff
//a direct copy of ItemMenu() except instead of using, you sell
void SellStuff()
{
	int bkg = duplicateimage(screen);
	int miniwin = GenerateWindow(70, 20);	
	int done;
	int itemmenuimg;
	int itemmenudesc;
	int i;
	int pointerpos;
	int scroll;
	

	itemmenuimg = GenerateWindow(200, 150);
	itemmenudesc = GenerateWindow(200, 20);
	
	pointerpos=0;
	
	done=0;
	scroll=0;
	
	while(done==0)
	{
		Blit(0, 0, bkg, screen);
		DrawMenu(itemmenuimg, 20, 30, "Item");
		DrawMenu(itemmenudesc, 20, 190, "Description");
		DrawMenu(miniwin, 235, 20, "Bling"); //money window
		
		PrintString(243, 28, screen, nFont, "Bling:");
		PrintString(280, 28, screen, nFont, str(nBling));

		for(i=scroll;i<scroll+ITEM_MENU_SIZE;i++)
		{
			if(nItemIndex[i] >= 0 && nItemCount[i] > 0)
			{
				//item name count, and sell value
				PrintString(36, 38+(8*(i-scroll)), screen, nFont, Items[nItemIndex[i]].sName);
				PrintString(160, 38+(8*(i-scroll)), screen, nFont, str(nItemCount[i]));
				PrintString(180, 38+(8*(i-scroll)), screen, nFont, str(Items[nItemIndex[i]].nCost/2));
			}
		}
		
		//desc text
		PrintString(28, 198, screen, nFont, Items[nItemIndex[pointerpos+scroll]].sDescription);
		
		//pointer
		TBlit(28, 38+(8*pointerpos), nPointer, screen);
		
		ShowPage();
		
		if(b1)
		{
			unpress(1);
			if(ChoiceMenu("Sell?", 70, 45))
			{
				//give the playa chedda
				nBling += Items[nItemIndex[pointerpos+scroll]].nCost/2;
				//remove the item from inventory
				RemoveItem(nItemIndex[pointerpos+scroll], 1);
			}
		}
		
		if(b2)
		{
			done=1;
			unpress(2);
		}
		
		if(up)
		{
			if(pointerpos>0)
			{
				pointerpos--;
				playsound(nMenuMoveSound, 100);
			}
			else
			{
				scroll--;
				if(scroll<0)
				{
					scroll=0;
				}
			}
			unpress(5);
		}

		if(down)
		{
			if(pointerpos<ITEM_MENU_SIZE)
			{
				pointerpos++;
				playsound(nMenuMoveSound, 100);
			}
			else
			{
				scroll++;
				if(scroll > MAX_ITEMS-ITEM_MENU_SIZE)
				{
					scroll = MAX_ITEMS-ITEM_MENU_SIZE;
				}
			}
			unpress(6);
		}
	}
	
	FreeImage(itemmenuimg);
	FreeImage(bkg);
}